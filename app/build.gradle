apply plugin: 'com.android.application'
apply plugin: 'realm-android'
apply plugin: 'me.tatarka.retrolambda'
apply plugin: 'com.neenbedankt.android-apt'

android {
    compileSdkVersion 25
    buildToolsVersion "24.0.3"

    dataBinding {
        enabled true
    }

    defaultConfig {
        //TODO: Change package name when using this template.
        applicationId "work.vuong.clocky"
        minSdkVersion 17
        targetSdkVersion 25
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            //TODO: set to true
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    // If you need to add more flavors, consider using flavor dimensions.
    productFlavors {
        stage {
            applicationIdSuffix = ".stage"
        }
        prod {

        }
        mock {
            applicationIdSuffix = ".mock"
        }
    }

    //Remove mock release
    android.variantFilter { variant ->
        if(variant.buildType.name.equals('release')
                && variant.getFlavors().get(0).name.equals('mock')) {
            variant.setIgnore(true);
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

// Define dependency versions
ext {
    supportLibraryVersion = '25.0.0'
    espressoVersion = '2.2.2'
    junitVersion = '4.12'
    daggerVersion = '2.2'
    rxVersion = '1.2.1'
    retrofitVersion = '2.1.0'
    okhttpVersion = '3.4.1'
    picassoVersion = '2.5.2'
    rxBindingVersion = '0.4.0'
    mockitoVersion = '1.10.19'
    powerMockito = '1.6.2'
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])

    //Support library
    compile "com.android.support:appcompat-v7:$supportLibraryVersion"
    compile "com.android.support:design:$supportLibraryVersion"
    compile "com.android.support:recyclerview-v7:$supportLibraryVersion"
    compile "com.android.support:cardview-v7:$supportLibraryVersion"

    //Dagger for injection
    compile "com.google.dagger:dagger:$daggerVersion"
    apt "com.google.dagger:dagger-compiler:$daggerVersion"

    //RxJava for reactive/functional programming
    compile "io.reactivex:rxjava:$rxVersion"
    compile "io.reactivex:rxandroid:$rxVersion"

    //Retrofit for network
    compile "com.squareup.retrofit2:retrofit:$retrofitVersion"
    compile "com.squareup.retrofit2:converter-gson:$retrofitVersion"
    compile "com.squareup.retrofit2:adapter-rxjava:$retrofitVersion"
    compile "com.squareup.okhttp3:logging-interceptor:$okhttpVersion"

    //Fetching images
    compile "com.squareup.picasso:picasso:$picassoVersion"

    //RxBinding for views
    compile "com.jakewharton.rxbinding:rxbinding:$rxBindingVersion"

    //Testing
    testCompile "org.mockito:mockito-all:$mockitoVersion"
    testCompile "junit:junit:$junitVersion"
    testCompile "org.powermock:powermock-module-junit4:$powerMockito"
    testCompile "org.powermock:powermock-api-mockito:$powerMockito"
    androidTestCompile("com.android.support.test.espresso:espresso-core:$espressoVersion", {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
}